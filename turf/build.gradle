plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

kotlin {
    jvm()
    js {
        browser {
        }
        nodejs {
        }
    }
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64

    linuxX64("native")
    mingwX64("mingw")
    macosX64("macos")
    ios("ios")

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                api project(":geojson")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation project(":geojson-dsl")
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        nativeMain {
        }
        nativeTest {
        }

        configure([macosMain, iosMain, mingwMain]) {
            dependsOn nativeMain
        }
        configure([macosTest, iosTest, mingwTest]) {
            dependsOn nativeTest
        }
    }
}


task copyTestResourcesForJs(type: Copy) {
    from "$projectDir/src/commonTest/resources"
    into "${rootProject.buildDir}/js/packages/${rootProject.name}-${project.name}-jsLegacy-test/src/commonTest/resources"
}

task copyTestResourcesForJsIr(type: Copy) {
    from "$projectDir/src/commonTest/resources"
    into "${rootProject.buildDir}/js/packages/${rootProject.name}-${project.name}-jsIr-test/src/commonTest/resources"
}

jsIrNodeTest.dependsOn copyTestResourcesForJsIr
jsLegacyTest.dependsOn copyTestResourcesForJs

jsIrBrowserTest.enabled = false
jsLegacyBrowserTest.enabled = false

apply from: rootProject.file("dokka.gradle")
apply plugin: "com.vanniktech.maven.publish"

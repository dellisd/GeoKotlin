plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

apply plugin: "com.vanniktech.maven.publish"
apply plugin: 'maven-publish'

kotlin {
    jvm()
    js {
        browser {
        }
        nodejs {
        }
    }
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64

    mingwX64("native")
    linuxX64("linux")
    macosX64("macos")
    ios("ios")

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                api project(":geojson")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation project(":geojson-dsl")
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        nativeMain {
        }
        nativeTest {
        }

        configure([linuxMain, macosMain, iosMain]) {
            dependsOn nativeMain
        }
        configure([linuxTest, macosTest, iosTest]) {
            dependsOn nativeTest
        }
    }
}


task copyTestResourcesForJs(type: Copy) {
    from "$projectDir/src/commonTest/resources"
    into "${rootProject.buildDir}/js/packages/${rootProject.name}-${project.name}-test/src/commonTest/resources"
}

jsNodeTest.dependsOn copyTestResourcesForJs

jsBrowserTest.enabled = false

apply from: rootProject.file("dokka.gradle")
